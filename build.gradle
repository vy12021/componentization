// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    android_version = '7.0.0-alpha12'
    kotlin_version = '1.4.32'
  }
  repositories {
    maven {
      name "plugin"
      url "https://nexus.bhbcode.com/repository/android-plugin/"
      credentials {
        username mavenUsername
        password mavenPassword
      }
      authentication {
        basic(BasicAuthentication)
      }
    }
    google()
    mavenCentral()
    gradlePluginPortal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
    maven { url 'https://maven.aliyun.com/repository/google' }
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$android_version"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    classpath 'com.sensorsdata.analytics.android:android-gradle-plugin2:3.2.12'
  }
}

plugins {
  id 'org.javamodularity.moduleplugin' version '1.7.0' apply false
}

apply plugin: 'componentization'

componentization {
  debugMode = true
  incremental = true
  resourcesDir = "build/abccc/out"
  includeModules = ['^libra.*', '^viewpager.*']
  includeJars = ['clase']
  includePackages = ['com.bhb.android']
  excludePackages = ['kotlin', 'kotlinx', 'android', 'androidx', 'org.jetbrains']
}

ext {

  minSdkVersion = 19
  targetSdkVersion = 28
  compileSdkVersion = 30
  buildToolsVersion = '30.0.3'

  applicationId = "com.bhb.android.componentization"
  versionName = "1.0.0"
  versionCode = 100000000

  sourceCompatibilityVersion = JavaVersion.VERSION_11
  targetCompatibilityVersion = JavaVersion.VERSION_11

}

allprojects {project ->
  repositories {
    maven {
      name "android"
      url "https://nexus.bhbcode.com/repository/android/"
      credentials {
        username mavenUsername
        password mavenPassword
      }
      authentication {
        basic(BasicAuthentication)
      }
    }
    google()
    mavenCentral()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
    maven { url 'https://maven.aliyun.com/repository/google' }
  }
}

subprojects {

  afterEvaluate {
    def plugins = getPlugins()
    def androidLibrary = plugins.hasPlugin('com.android.library')
    def androidApp = plugins.hasPlugin('com.android.application')
    def kotlinSupport = plugins.hasPlugin('kotlin-kapt')

    if (!(androidLibrary || androidApp)) {
      sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
      targetCompatibility = rootProject.ext.targetCompatibilityVersion
      return
    }

    dependencies {
      api fileTree(dir: "${project.projectDir}/libs", include: ['*.jar'])
      implementation 'androidx.appcompat:appcompat:1.2.0'
      implementation 'androidx.annotation:annotation:1.1.0'
      testImplementation 'junit:junit:4.13.2'
      androidTestImplementation 'androidx.test.ext:junit:1.1.2'
      androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    }

    if (kotlinSupport) {
      kapt {
        useBuildCache = true
      }
    }

    android {
      compileSdkVersion rootProject.ext.compileSdkVersion
      buildToolsVersion rootProject.ext.buildToolsVersion
      defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        if (androidApp) {
          applicationId rootProject.ext.applicationId
          versionCode rootProject.ext.versionCode
          versionName rootProject.ext.versionName
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
      }
      sourceSets {
        main {
          java.srcDirs += ['src/main/kotlin']
          jni.srcDirs = ['src/main/cpp']
          jniLibs.srcDirs += ['libs']
        }
      }
      dexOptions {
        preDexLibraries true
      }
      aaptOptions {
        cruncherEnabled false
      }
      compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
      }
      if (kotlinSupport) {
        kotlinOptions {
          freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
          jvmTarget = '1.8'
        }
      }
      lintOptions {
        abortOnError false
      }
      testOptions {
        unitTests.returnDefaultValues = true
      }
      packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
      }
    }
  }

}

task clean(type: Delete) {
  delete rootProject.buildDir
}
